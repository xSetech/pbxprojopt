// Grammar for the OpenStep/Next-style property list as used by XCode

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

/* Comments, file encoding, and special markers */

FileEncoding = ${
    "UTF8"
}

FileEncodingSpecifier = _{
    "!$*" ~ FileEncoding ~ "*$!"
}

LineCommentEnd = _{
    NEWLINE | EOI
}

LineCommentValue = ${
    (!NEWLINE ~ ANY)+
}

LineComment = @{
    "//" ~ WHITESPACE* ~ (FileEncodingSpecifier | LineCommentValue?) ~ LineCommentEnd
}

BlockCommentValue = ${
    (!(WHITESPACE | "*/") ~ ANY ~ (WHITESPACE+ ~ !"*/" ~ ANY)?)+
}

BlockComment = {
    "/*" ~ BlockCommentValue? ~ "*/"
}

Comment = _{
    LineComment | BlockComment
}

/* Strings */

QuotedStringCharacters = @{
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "n" | "r" | "t")
}

QuotedString = @{
    "\"" ~ (QuotedStringCharacters)* ~ "\""
}

UnquotedString = @{
    (ASCII_ALPHANUMERIC | "_" | "$" | "+" | "/" | ":" | "." | "-")+
}

String = @{
    QuotedString | UnquotedString
}

/* Arrays */

ArrayEntry = {
    Comment* ~
    (
        Array
        | Dictionary
        | String
    )~
    Comment*
}

Array = {
    ("(" ~ ")")
    | ("(" ~ ArrayEntry ~ ("," ~ ArrayEntry)* ~ ","? ~ ")")
}

/*  Dictionaries and key-value pairs */

DictKey = {
    Comment* ~ String ~ Comment*
}

DictVal = {
    Comment* ~
    (
        Array
        | Dictionary
        | String
    )~
    Comment*
}

DictEntry = {
    DictKey ~ "=" ~ DictVal ~ ";" ~ Comment*
}

Dictionary = {
    ("{" ~ "}")
    | ("{" ~ DictEntry+ ~ "}")

}

/* File specification */

File = {
    SOI ~
    (
        Comment
        | Array
        | Dictionary
        | String
        )+ ~
    EOI
}

// eof